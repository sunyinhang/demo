import org.springframework.util.StringUtils

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

group = 'com.haiercash'
version = StringUtils.trimWhitespace(new File("${projectDir.getAbsolutePath()}${File.separator}..${File.separator}VERSION").text)
sourceCompatibility = 1.8

buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
        springCloudVersion = 'Dalston.SR3'
        isReleaseVersion = project.hasProperty('release')
    }
    repositories {
        mavenLocal()
        maven { url 'http://10.164.194.139:8081/nexus/content/repositories/public/' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

repositories {
    mavenLocal()
    maven { url 'http://10.164.194.139:8081/nexus/content/repositories/public/' }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    compile project(':haiercash-core')

    //==依赖==
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    compile('org.springframework.cloud:spring-cloud-starter-feign')
    compile('org.springframework.cloud:spring-cloud-starter-zuul')
    compile('org.springframework.session:spring-session')
    //数据库
    compile('com.oracle:ojdbc6:11g')
    compile('com.alibaba:druid-spring-boot-starter:1.1.5')
    compile('com.haiercash:haiercash-mybatis:1.0.4')
    //文件
    compile('org.apache.commons:commons-vfs2:2.2')
    compile('commons-net:commons-net:3.6')
    compile('com.jcraft:jsch:0.1.54')
    //支持
    compile('org.json:json')
    //==编译时==
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    compileOnly('org.projectlombok:lombok')
    //==测试时==
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.projectlombok:lombok')
}

jar {
    baseName = project.name
    version = project.isReleaseVersion ? project.version : "${project.version}-SNAPSHOT"
}

bootRepackage {
    enabled = false
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.isReleaseVersion ? project.version : "${project.version}-SNAPSHOT"
            //
            from components.java
            //
            pom.withXml {
                asNode().appendNode('name', POM_NAME)
                asNode().appendNode('description', POM_DESCRIPTION)
                asNode().appendNode('url', POM_URL)
            }
        }
    }
    repositories {
        maven {
            url project.isReleaseVersion ? NEXUS_RELEASE_REPO_URL : NEXUS_SNAPSHOT_REPO_URL
            credentials {
                username NEXUS_USERNAME
                password NEXUS_PASSWORD
            }
        }
    }
}
